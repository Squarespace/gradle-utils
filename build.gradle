buildscript {
    repositories {
        mavenCentral()
        maven { url "http://clojars.org/repo" }
    }

    dependencies {
        classpath "de.kotka.gradle:gradle-plugin:1.2.0"
    }
}

version = "0.2.2-SNAPSHOT"
group   = "de.kotka.gradle"

description = "A set of utilities to ease the life of gradle plugin developers"

ext.kotka = [
    repository:     "kotkade/gradle-utils",
    wrapperVersion: "1.10"
]

apply plugin: "kotka"
apply plugin: "groovy"

sourceSets {
    macros {
        compileClasspath = project.sourceSets.main.compileClasspath
    }
}

compileMacrosGroovy {
    sourceCompatibility = "1.6"
    targetCompatibility = "1.6"
}

compileGroovy {
    sourceCompatibility = "1.6"
    targetCompatibility = "1.6"

    classpath = classpath.plus project.sourceSets.macros.output
}

compileTestGroovy {
    classpath = classpath.plus project.sourceSets.macros.output
}

test {
    classpath = classpath.plus project.sourceSets.macros.output
}

repositories {
    mavenCentral()
    maven { url "http://clojars.org/repo" }
}

dependencies {
    compile localGroovy()
    compile gradleApi()

    compile "de.kotka.groovy:zweig:0.4.0"

    testCompile('org.spockframework:spock-core:0.7-groovy-1.8') {
        exclude group: "org.codehaus.groovy", module: "groovy-all"
    }
    testCompile 'com.github.goldin:spock-extensions:0.1.4'
}

groovydoc {
    source project.sourceSets.macros.groovy
}

jar {
    from project.sourceSets.macros.output
}

sourcesJar {
    from project.sourceSets.macros.allSource
}
