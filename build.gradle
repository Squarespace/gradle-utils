plugins {
  id 'net.researchgate.release' version '2.3.4'
}

task wrapper(type: Wrapper) {
  gradleVersion = '2.12'
}

apply plugin: "groovy"
apply plugin: 'maven-publish'

group = "de.kotka.gradle"

repositories {

  mavenLocal()
  
  maven {
    url RELEASE_REPOSITORY_URL
  }
  
  mavenCentral()
  maven { url "http://clojars.org/repo" }
}

sourceSets {
  macros {
    compileClasspath = project.sourceSets.main.compileClasspath
  }
}

compileMacrosGroovy {
  sourceCompatibility = '1.8'
  targetCompatibility = '1.8'
}

compileGroovy {
  sourceCompatibility = '1.8'
  targetCompatibility = '1.8'

  classpath = classpath.plus project.sourceSets.macros.output
}

compileTestGroovy {
  classpath = classpath.plus project.sourceSets.macros.output
}

test {
  classpath = classpath.plus project.sourceSets.macros.output
}

dependencies {
  compile localGroovy()
  compile gradleApi()

  compile "de.kotka.groovy:zweig:0.4.0"

  testCompile 'org.codehaus.groovy:groovy-all:2.4.4'
  testCompile('org.spockframework:spock-core:1.0-groovy-2.4') {
    exclude group: 'org.codehaus.groovy', module: 'groovy-all'
  }
  testCompile 'com.github.goldin:spock-extensions:0.1.4'
}

jar {
  from project.sourceSets.macros.output
}

// NOTE: The 'RELEASE_REPOSITORY_URL', 'MAVEN_CENTRAL_USERNAME' 
//       and 'MAVEN_CENTRAL_PASSWORD' values are stored in the 
//       ~/.gradle/gradle.properties file.

publishing {

  repositories {
    maven {
      url RELEASE_REPOSITORY_URL
    }
  }
  
  publications {
    sqsp(MavenPublication) {
      groupId "com.squarespace.${group}"
      from components.java
    }
  }
}

release {
  git {
    requireBranch = 'master'
  }
  
  failOnUpdateNeeded = false
  failOnSnapshotDependencies = false
}

createReleaseTag.dependsOn publish

